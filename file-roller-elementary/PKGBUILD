# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=file-roller
pkgname=file-roller-elementary
pkgver=3.42.0
pkgrel=1
pkgdesc="Create and modify archives, with elementaryOS customizations"
url="https://wiki.gnome.org/Apps/FileRoller"
arch=(x86_64)
license=(GPL)
conflicts=(file-roller)
depends=(libhandy dconf libarchive file json-glib libnotify zip unzip)
makedepends=(yelp-tools git libnautilus-extension meson appstream-glib)
optdepends=('p7zip: 7z, arj, exe and encrypted zip files support'
            'unrar: better RAR archive support'
            'unace: ACE archive support'
            'lrzip: lrzip archive support'
            'squashfs-tools: squashfs image support')
groups=(gnome)
options=(debug)
_commit=f6f1798376cb9b64ee865d354f1c330ea3e56bcd  # tags/3.42.0^0
_commit_elementary=76f7024dd4424a574dfe67ec26ffe2c8e666d343
source=(
  "git+https://gitlab.gnome.org/GNOME/file-roller.git#commit=$_commit"
  "elementary-fileroller::git+https://github.com/elementary/fileroller#commit=$_commit_elementary"
)
sha256sums=(
  'SKIP'
  'SKIP'
)

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $_pkgname
  # elementaryOS patches
  patch --forward --strip=1 --input="${srcdir}/elementary-fileroller/msandova:show-uri-on-window.patch"
  patch --forward --strip=1 --input="${srcdir}/elementary-fileroller/fr-remove-open-with.patch"
}

build() {
  arch-meson $_pkgname build
  arch-meson elementary-fileroller build-elementary
  meson compile -C build
  meson compile -C build-elementary
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Remove upstream GNOME icons.
  rm -rf "$pkgdir/usr/share/icons/hicolor/scalable"
  rm -rf "$pkgdir/usr/share/icons/hicolor/symbolic"
  meson install -C build-elementary --destdir "$pkgdir"
}
